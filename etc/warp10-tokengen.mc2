//
//   Copyright 2019  SenX S.A.S.
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.
//


UUID 'owner' STORE # Generate a new UUID for owner and producer
'io.warp10.bootstrap' 'application' STORE
365 100 * d MSTU / 'ttl' STORE # Generate a TTL of 100 years (note that TOKENGEN expects a TTL in milliseconds)

#
# Generate read token
#
{
  'type' 'READ'
  'owner' $owner
  'application' $application
  'ttl' $ttl
  
  'applications' [ $application ]
  'owners' [ $owner ]
  'producers' [ $owner ]
}
{{secret}}
TOKENGEN

DUP
'ident' GET 'readIdent' STORE
'token' GET 'readToken' STORE

#
# Generate write token
#
{
  'type' 'WRITE'
  'owner' $owner
  'producer' $owner
  'application' $application
  'ttl' $ttl
}
{{secret}}
TOKENGEN

DUP
'ident' GET 'writeIdent' STORE
'token' GET 'writeToken' STORE


#
# Format token to match initial.tokens pattern
#

{
  "read" {
    "token" $readToken
    "tokenIdent" $readIdent
    "ttl" $ttl
    "application" $application
    "applications" [
      $application
    ]
    "owners" [
      $owner
    ]
    "producer" $owner
    "producers" [
      $owner
    ]
  }
  "write" {
    "token" $writeToken
    "tokenIdent" $writeIdent
    "ttl" $ttl
    "application" $application
    "owner" $owner
    "producer" $owner
  }
}
